---
title: Scripts
layout: doc
permalink: /doc/reference/script
section: documentation
subsection: reference
summary: Pallet uses a script DSL called stevdeore to generate shell scripts
         that can target different distributions and operating systems.
---

Pallet scripts are written using an embedding of shell script in
[Clojure](http://clojure.org), and allows for abstraction of operating system
variations.

At it's simplest, the `script` macro takes one or more forms and converts them
to shell script.

{% highlight clojure %}
(use '[pallet.stevedore :only [script with-script-language]])
(require 'pallet.stevedore.bash) ;; for bash output

(with-script-language :pallet.stevedore.bash/bash
  (script
    ("ls")))
{% endhighlight %}

results in:

{% highlight bash %}
ls
{% endhighlight %}

For the ease of trying things at the REPL, the following examples allow you to drop
the `with-script-language` wrapping form. N.B. do not do this in your code!

{% highlight clojure %}
(.bindRoot #'pallet.stevedore/*script-language* :pallet.stevedore.bash/bash)
{% endhighlight %}

## Variables
There is an obvious mismatch between Clojure's immutable data approach, and
shell script's mutable variables.

### Substitution
For accessing the value of a script variable, stevedore uses `deref` or `@`, which
are also used for command substitution.

{% highlight clojure %}
(script (deref TMPDIR))
{% endhighlight %}

results in:

{% highlight bash %}
${TMPDIR}
{% endhighlight %}

If the variable isn't set, a default can be supplied.
{% highlight clojure %}
(script @TMPDIR-/tmp)
{% endhighlight %}

results in:

{% highlight bash %}
${TMPDIR-/tmp}
{% endhighlight %}


### Assignment
Values can be defined with `set!`.
{% highlight clojure %}
(script (set! x 1))
{% endhighlight %}

results in:

{% highlight bash %}
x=1
{% endhighlight %}


## Command Substitution

Similar to variable substitution, command substitution uses `deref`.

{% highlight clojure %}
(script (set! R @(cat "/etc/redhat-release")))
{% endhighlight %}

results in:

{% highlight bash %}
R=$(cat /etc/redhat-release)
{% endhighlight %}

## Loops
Shell script `for` loops are mapped to clojure's `doseq`.

{% highlight clojure %}
(script
 (doseq [x ["a" "b" "c"]]
   (println @x)))
{% endhighlight %}

results in:

{% highlight bash %}
for x in a b c; do
  echo ${x}
done
{% endhighlight %}

You can also combine command substitution with loops like this

{% highlight clojure %}
(script
 (doseq [x @(ls "/home/")]
   (println @x)))
{% endhighlight %}

{% highlight bash %}
for x in $(ls /home/); do
  echo ${x}
done
{% endhighlight %}

## Functions

Calling a function follows clojure's normal syntax.

{% highlight clojure %}
(script (foo x y))
{% endhighlight %}

 results in:

{% highlight bash %}
foo x y
{% endhighlight %}

Defining a function follows a simplified version of clojure's `defn`, and does
not allow for destructuring. Arguments are named and automatically mapped to the
shell function's numbered arguments.

{% highlight clojure %}
(script (defn foo [x y] (bar x)))
{% endhighlight %}

results in:

{% highlight bash %}
script function foo() {
  x=$1
  y=$2
  bar x
}
{% endhighlight %}

## Arrays

Array literals are generated from clojure's `vector` notation.

{% highlight clojure %}
(script [1 "2" (quoted 3) :four])
{% endhighlight %}

results in:
{% highlight bash %}
(1 2 "3" four)
{% endhighlight %}

Array access uses `aget` and `aset`.

{% highlight clojure %}
(script (aget foo 1))
{% endhighlight %}

results in:

{% highlight bash %}
${foo[1]}
{% endhighlight %}

{% highlight clojure %}
(script (aset foo 1 :foo))
{% endhighlight %}

results in:

{% highlight bash %}
foo[1]=foo
{% endhighlight %}

## Unquoting

One advantage of embedding script in clojure is that we can escape back to
clojure to substitute in values.

{% highlight clojure %}
(let [tmp "/tmp"]
  (script (ls ~tmp)))
{% endhighlight %}

results in:

{% highlight bash %}
ls /tmp
{% endhighlight %}

### Expressions
We can also unquote arbitrary clojure expressions.
{% highlight clojure %}
(script (ls ~(str "/" "tmp")))
{% endhighlight %}

results in:

{% highlight bash %}
ls /tmp
{% endhighlight %}

### Splicing
Sometimes we want to add the contents of a collection into a script, without
demarking the boundaries of the collection.
{% highlight clojure %}
(let [files ["a" "b" "c"]]
  (script (ls ~@files)))
{% endhighlight %}

results in:

{% highlight bash %}
ls a b c
{% endhighlight %}

## Script functions

Script functions are defined with `pallet.script/defscript` and provide multimethods
for shell script generation.  In pallet this is used to abstract away script
differences between operating systems.

{% highlight clojure %}
(require
 'pallet.stevedore.bash)
(use
 '[pallet.script :only [defscript defimpl with-script-context]]
 '[pallet.stevedore :only [script with-script-language]])

(defscript ls [& args])
(defimpl ls :default [& args]
  (ls ~@args))
(defimpl ls [:windows] [& args]
  (dir ~@args))

(with-script-language :pallet.stevedore.bash/bash
  (with-script-context [:windows]
    (script (~ls a b c))))
{% endhighlight %}

results in:

{% highlight bash %}
dir a b c
{% endhighlight %}

## Scripting Guide

An excellent reference on shell scripting is available at
[http://tldp.org/LDP/abs/html/](http://tldp.org/LDP/abs/html/).
